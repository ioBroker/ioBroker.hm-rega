!# datapoints.fn 1.9
!#
!# Dieses Homematic-Script gibt eine Liste aller Datenpunkte (ausser WORKING/DIRECTION) als JSON String aus
!#
!# 3'2013-9'2014 hobbyquaker https://github.com/hobbyquaker
!#

string sDevId;
string sChnId;
string sDPId;
string sValue;
string sChnId;
string sDPId;
boolean dpFirst = true;
Write('{');
foreach (sDevId, root.Devices().EnumUsedIDs()) {
    object oDevice   = dom.GetObject(sDevId);
    boolean bDevReady = oDevice.ReadyConfig();
    if (bDevReady) {
        foreach (sChnId, oDevice.Channels()) {
            object oChannel = dom.GetObject(sChnId);
            foreach(sDPId, oChannel.DPs().EnumUsedIDs()) {
                object oDP = dom.GetObject(sDPId);
                if (oDP && oDP.Timestamp().ToInteger() > 0) {
                    if (oDP.TypeName() != "VARDP") {
                        if (dpFirst) {
                          dpFirst = false;
                        } else {
                          WriteLine(',');
                        }
                        string sValueType = oDP.ValueType();
                        Write('"');
                        WriteURL(oDP.Name());
                        Write('":');
                        if (sValueType == 20) {
                            Write('"');
                            WriteURL(oDP.Value());
                            Write('"');
                        } else {
                            sValue = oDP.Value();
                            if (sValueType == 2) {
                                if (sValue) {
                                    Write("true");
                                } else {
                                    Write("false");
                                }
                            } else {
                               if (sValue == "") {
                                    Write("0");
                               } else {
                                    !# see https://github.com/jens-maus/RaspberryMatic/issues/897
                                    if ((oDP.Name().EndsWith('.RSSI_PEER') || oDP.Name().EndsWith('.RSSI_DEVICE')) && sValueType == 8) {
                                      sValue = sValue - 256;
                                    }
                                    Write(sValue);
                               }
                            }
                        }
                    }
                }
            }
        }
    }
}
Write('}');
